; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ROM Properties Page Shell Extension"
#define MyAppVersion "2.6+"
#define MyAppPublisher "GerbilSoft"
#define MyAppURL "https://github.com/GerbilSoft/rom-properties"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E39AEEA4-836C-4408-8A04-4456C9A23816}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
; NOTE: rom-properties doesn't actually change file associations, but it does register
; handlers for various types, so we need to tell Windows Explorer to refresh.
ChangesAssociations=yes
Compression=lzma2/max
DefaultDirName={pf}\rom-properties
DefaultGroupName=ROM Properties
LicenseFile=..\LICENSE
PrivilegesRequired=admin
OutputBaseFilename=SetupRomProperties
SolidCompression=yes
WizardStyle=modern

; Enable 64-bit mode on amd64 and arm64.
ArchitecturesAllowed=x86 x64
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Code]

function InitializeSetup(): Boolean;
begin
  if GetWindowsVersion() >= $06010000 then
  begin
    MsgBox('This ROM Properties Page Shell Extension installer is designed for Windows XP, Windows Server 2003, and Windows Vista only.' + #13#10#13#10 +
      'You can get the Windows 7/8/10/11 version at:' + #13#10 +
      'https://github.com/GerbilSoft/rom-properties', mbCriticalError, MB_OK)
    Result := False
  end
  else
  begin
    Result := True
  end;
end;

function ShouldInstallDll_i386(): Boolean;
begin
  case ProcessorArchitecture of
    paX86: Result := True;
    paX64: Result := True;
  else
    Result := False;
  end;
end;

function ShouldInstallDll_amd64(): Boolean;
begin
  case ProcessorArchitecture of
    paX64: Result := True;
  else
    Result := False;
  end;
end;

function ShouldInstallDll_i386Only(): Boolean;
begin
  case ProcessorArchitecture of
    paX86: Result := True;
  else
    Result := False;
  end;
end;

function ShouldInstallDll_amd64Only(): Boolean;
begin
  case ProcessorArchitecture of
    paX64: Result := True;
  else
    Result := False;
  end;
end;

[Files]
; InnoSetup must be run from the pkg_windows directory, as part of package.cmd.
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

Source: "..\pkg_windows\build.i386\bin\Release\fmt-12.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\libgnuintl-8.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\libpng16.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\lz4.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\minilzo.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\minizip.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\pugixml.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\zlib1.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\romdata-8.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\rom-properties.dll"; DestDir: "{app}\i386"; Flags: ignoreversion uninsrestartdelete 32bit regserver; Check: ShouldInstallDll_i386
Source: "..\pkg_windows\build.i386\bin\Release\rp-download.exe"; DestDir: "{app}\i386"; Flags: ignoreversion 32bit; Check: ShouldInstallDll_i386

Source: "..\pkg_windows\build.amd64\bin\Release\fmt-12.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\libgnuintl-8.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\libpng16.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\lz4.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\minilzo.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\minizip.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\pugixml.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\zlib1.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\romdata-8.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\rom-properties.dll"; DestDir: "{app}\amd64"; Flags: ignoreversion uninsrestartdelete 64bit regserver; Check: ShouldInstallDll_amd64
Source: "..\pkg_windows\build.amd64\bin\Release\rp-download.exe"; DestDir: "{app}\amd64"; Flags: ignoreversion 64bit; Check: ShouldInstallDll_amd64

; Install rpcli.exe and rp-config.exe in the root directory using the matching system architecture.
Source: "..\pkg_windows\build.i386\bin\Release\rpcli.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: ShouldInstallDll_i386Only
Source: "..\pkg_windows\build.amd64\bin\Release\rpcli.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: ShouldInstallDll_amd64

Source: "..\pkg_windows\build.i386\bin\Release\rp-config.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: ShouldInstallDll_i386Only
Source: "..\pkg_windows\build.amd64\bin\Release\rp-config.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: ShouldInstallDll_amd64Only

; Data files
Source: "..\pkg_windows\build.i386\bin\amiibo-data.bin"; DestDir: "{app}"; Flags: ignoreversion

; Property Description Schemas
; TODO: Register the .propdesc file using PSRegisterPropertySchema().
Source: "..\src\win32\res\rom-properties.propdesc"; DestDir: "{app}"; Flags: ignoreversion

; Documentation
Source: "..\doc\COMPILING.md"; DestDir: "{app}\doc"; Flags: ignoreversion
Source: "..\doc\keys.conf.example"; DestDir: "{app}\doc"; Flags: ignoreversion
Source: "..\doc\rom-properties.conf.example"; DestDir: "{app}\doc"; Flags: ignoreversion
Source: "..\README.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\NEWS.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\LICENSE"; DestDir: "{app}"; Flags: ignoreversion
